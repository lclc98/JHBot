plugins {
    id 'java'
    id 'com.google.cloud.tools.jib' version '2.4.0'
}

version = "${project_version}" + (isSnapshot() ? '.' + getBuildNumber() : '')
group = "${project_group}"
archivesBaseName = "${project_name}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.jfrog.org/artifactory/libs-release"}
}

dependencies {
    implementation group: 'com.discord4j', name: 'discord4j-core', version: '3.0.14'
    implementation group: 'com.github.twitch4j', name: 'twitch4j', version: '1.0.0-alpha.18'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'JHBot',
                'Main-Class': 'com.plumealerts.jhbot.JHBot'
    }
}

jib {
    to {
        image = "plumealerts/jhbot:" + version
        tags = ['latest' + (isSnapshot() ? "-snapshot" : '')]
        auth {
            username = System.getenv("DOCKER_USERNAME")
            password = System.getenv("DOCKER_PASSWORD")
        }
    }
    container {
        jvmFlags = ['-Xms512m']
    }
}

static String getBuildNumber() {
    return System.getenv("GITHUB_RUN_NUMBER") ?: "0"
}

static boolean isSnapshot() {
    String ref = System.getenv("GITHUB_REF");
    if (ref != null && ref.startsWith("refs/tags/v")) {
        return false
    }
    return true
}